plugins {
    id 'nebula.netflixoss' version '3.2.3'
}

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

apply plugin: 'osgi'
apply plugin: 'project-report'


subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'    

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
    }
    group = "com.netflix.${githubProjectName}"

    dependencies {
        compile "joda-time:joda-time:2.3"
        compile "org.slf4j:slf4j-api:1.7.6"
        compile "org.slf4j:slf4j-log4j12:1.7.21"
        compile "com.googlecode.json-simple:json-simple:1.1"
        compile "org.apache.httpcomponents:httpclient:4.2.1"
        compile "com.sun.jersey:jersey-core:1.11"
        testCompile "junit:junit:4.11"
    }

    // Maven-publish gradle bug workaround
    // https://github.com/Netflix/dyno/issues/138
    publishing {
        publications {
            nebula(MavenPublication) {
                pom.withXml {
                    configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
                        asNode().dependencies[0].dependency.find {
                            it.artifactId[0].text() == dep.moduleName &&
                                    it.groupId[0].text() == dep.moduleGroup
                        }.scope[0].value = 'compile'
                    }
                }
            }
        }
    }

    project.tasks.withType(Javadoc) {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

}

project(':dyno-core') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'
    
    dependencies {
        compile "org.apache.commons:commons-math:2.2"
        compile "commons-io:commons-io:2.4"
        compile "org.mockito:mockito-all:1.9.5"
        testCompile "junit:junit:4.11"
    }
}

project(':dyno-contrib') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'
    
    dependencies {
        compile  project(':dyno-core')
        compile "com.google.inject:guice:3.0"
        compile "com.netflix.archaius:archaius-core:0.5.6"
        compile "com.netflix.servo:servo-core:0.5.5"
        compile 'com.netflix.eureka:eureka-client:1.1.110'
    }
}

project(':dyno-memcache') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile  project(':dyno-core')
        compile  project(':dyno-contrib')
//        compile "net.spy:spymemcached:2.10.2+"
    }
}

project(':dyno-jedis') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile  project(':dyno-core')
        compile  project(':dyno-contrib')
        compile "redis.clients:jedis:2.8.1"
    }
}

project(':dyno-redisson') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile  project(':dyno-core')
        compile  project(':dyno-contrib')
        compile "org.redisson:redisson:1.1.0"
    }
}

project(':dyno-demo') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile  project(':dyno-core')
        compile  project(':dyno-contrib')
        compile  project(':dyno-memcache')
        compile  project(':dyno-jedis')
        compile  project(':dyno-recipes')
    }
}

project(':dyno-recipes') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile project(':dyno-core')
        compile project(':dyno-jedis')
    }
}
