plugins {
    id 'nebula.netflixoss' version '5.0.0'
}

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

apply plugin: 'osgi'
apply plugin: 'project-report'
apply plugin: 'maven-publish'

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    sourceCompatibility = 1.8

    repositories {
        jcenter()
    }
    group = "com.netflix.${githubProjectName}"

    dependencies {
        compile 'joda-time:joda-time:2.3'
        compile 'org.slf4j:slf4j-api:1.7.6'
        compile 'org.slf4j:slf4j-log4j12:1.7.21'
        compile 'com.googlecode.json-simple:json-simple:1.1'
        compile 'org.apache.httpcomponents:httpclient:4.2.1'
        compile 'com.sun.jersey:jersey-core:1.11'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'io.netty:netty-all:4.1.14.Final'
    }

    project.tasks.withType(Javadoc) {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

project(':dyno-core') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'
    
    dependencies {
        compile 'org.apache.commons:commons-math:2.2'
        compile 'org.apache.commons:commons-lang3:3.6'
        compile 'commons-io:commons-io:2.4'
    }
}

project(':dyno-contrib') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'
    
    dependencies {
        compile project(':dyno-core')
        compile 'com.google.inject:guice:4.1.0'
        compile 'com.netflix.archaius:archaius-core:0.7.6'
        compile 'com.netflix.servo:servo-core:0.12.17'
        compile 'com.netflix.eureka:eureka-client:1.8.6'
        compile 'org.apache.commons:commons-lang3:3.5'
        compile ('com.ecwid.consul:consul-api:1.2.1') {
            exclude group: 'org.apache.httpcomponents'
        }       
        testCompile 'com.pszymczyk.consul:embedded-consul:0.3.3'
    }
}

project(':dyno-memcache') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile project(':dyno-core')
        compile project(':dyno-contrib')
    }
}

project(':dyno-jedis') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile project(':dyno-core')
        compile project(':dyno-contrib')
        compile 'redis.clients:jedis:2.9.0'
    }
}

project(':dyno-client') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile project(':dyno-jedis')
    }
}


project(':dyno-redisson') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile project(':dyno-core')
        compile project(':dyno-contrib')
        compile 'org.redisson:redisson:1.1.0'
    }
}

project(':dyno-demo') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile project(':dyno-core')
        compile project(':dyno-contrib')
        compile project(':dyno-memcache')
        compile project(':dyno-jedis')
        compile project(':dyno-recipes')
    }
}

project(':dyno-recipes') {
    apply plugin: 'osgi'
    apply plugin: 'project-report'

    dependencies {
        compile project(':dyno-core')
        compile project(':dyno-jedis')
    }
}
